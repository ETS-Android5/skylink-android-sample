apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "sonar-runner"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "0.9.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Check environment variables for Archiva configuration (e.g CI environment)
if (System.getenv("ARCHIVA_URL") != null) {
    archiva_contextUrl = "$System.env.ARCHIVA_URL"
    println 'Used environment variable for Archiva URL'
}

if (System.getenv("ARCHIVA_USR") != null) {
    archiva_username = "$System.env.ARCHIVA_USR"
    println 'Used environment variable for Archiva Username'
}

if (System.getenv("ARCHIVA_PWD") != null) {
    archiva_password = "$System.env.ARCHIVA_PWD"
    println 'Used environment variable for Archiva Password'
}

project.ext.isReleaseVersion = !currentVersion.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenSdk(MavenPublication) {
            artifacts {
                groupId 'sg.com.temasys.skylink.sdk'
                artifactId 'skylink_sdk'
                version currentVersion
                artifact 'build/outputs/aar/skylink_sdk-release.aar'
            }
        }
    }
    repositories {
        maven {
            if (!project.ext.isReleaseVersion) {
                url archiva_contextUrl + "/snapshots"
            } else {
                url archiva_contextUrl + "/internal"
            }
            credentials {
                username archiva_username
                password archiva_password
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.projectKey", "AND"
        property "sonar.projectName", "Skylink Android SDK"
        property "sonar.projectVersion", currentVersion
        property "sonar.language", "java"
        property "sonar.sources", "src"
        property "sonar.binaries", "build"
        property "sonar.profile", "Temasys-Android"
    }
}